plugins {
    id 'com.github.sherter.google-java-format' version '0.8'
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    mavenCentral()
}

group = 'com.vgalloy'
version = '1.0-SNAPSHOT'

ext {
    javaVersion = '1.8'
    encoding = 'UTF-8'

    gatlingVersion = '3.1.3'
    springBootversion = '2.2.5.RELEASE'
    junitVersion = '5.6.0'
}

googleJavaFormat {
    toolVersion = '1.7'
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

tasks.withType(JavaCompile) {
    dependsOn ':googleJavaFormat'
    sourceCompatibility = "$javaVersion"
    targetCompatibility = "$javaVersion"
    options.encoding = "$encoding"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "${project.group}"
            artifactId "gatling-java-api"
            version "${project.version}"

            from components.java
        }
    }
}

dependencies {
    compile "io.gatling.highcharts:gatling-charts-highcharts:$gatlingVersion"

    testCompile "org.springframework.boot:spring-boot-starter-web:$springBootversion"
    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootversion"
    testCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testCompile "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

integrationTest.mustRunAfter test
check.dependsOn integrationTest
publishToMavenLocal.dependsOn check
