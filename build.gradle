apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    mavenCentral()
}

group = 'com.vgalloy'
version = '1.0-SNAPSHOT'

ext {
    javaVersion = '1.8'
    gatlingVersion = '3.0.3'
    springBootversion = '2.1.5.RELEASE'
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceCompatibility = "$javaVersion"
targetCompatibility = "$javaVersion"

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "${project.group}"
            artifactId "gatling-java-api"
            version "${project.version}"

            from components.java
        }
    }
}

dependencies {
    compile "io.gatling.highcharts:gatling-charts-highcharts:$gatlingVersion"

    testCompile "org.springframework.boot:spring-boot-starter-web:$springBootversion"
    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootversion"
}

integrationTest.mustRunAfter test
check.dependsOn integrationTest
publishToMavenLocal.dependsOn check
